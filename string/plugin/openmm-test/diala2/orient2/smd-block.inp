* test orientational restraints
* also, use block here to see how well the two dialas can actually
* overlap
*

set nrun 1
set outputdir .

!**********************************************************
mcom string ?numnode by 1
string colvar init
!**************************** replica variables ***********
set ncpu ?nstring
set me ?mestring
!**********************************************************
! read parameter and topology files
!set toppar  ~/charmm/parameters/c36
set toppar  .

string open unit 1 read form name "@TOPPAR/top_all22_prot.rtf"
read rtf card unit 1
close unit 1

string open unit 1 read form name "@TOPPAR/par_all22_prot.prm"
read param card unit 1
close unit 1

read sequ card 
*
1
ALAD

generate DIA setup warn

write psf card name diala.psf

ic edit
dihe  dia 1 clp   dia 1 nl   dia 1 ca   dia 1 crp -50
dihe  dia 1 nl   dia 1 ca   dia 1 crp   dia 1 nr 50
end

ic para
ic seed 1 CLP 1 NL 1 CA
ic build

mini conj nstep 100
!
energy
!
repl dia nrep 1 sele segid DIA end setup
! this creates a replica of diala with segid dia1
! change its position, but keep orientation
coor copy comp
! put each replica into a separate block and turn off their interaction
block 2
 call 1 sele segid dia end
 call 2 sele segid dia1 end
 coef 1 1 1.0
 coef 1 2 0.0
 coef 2 2 1.0
end
!
energy ! should be exactly twice the previous value
!stop
!
set dx 0
set dy 0
!
scalar X add @dx sele segid dia1 end
scalar YCOMP add @dy sele segid dia1 end
!
! mark different replicas in wmain array
scalar wmain set 1 sele segid dia end
scalar wmain set 2 sele segid dia1 end
!
write psf card name diala2.psf
write psf card name diala2-x.psf xplor
write coor pdb name diala2-0.pdb
write coor pdb name diala2-1.pdb comp
energy cutnb 24 ctonnb 20 ctofnb 22
! re-read coords to account for round off error when comparing to other codes
read coor pdb name diala2-0.pdb resid

energy
stop
!===================================================================================
! now set up orientational and positional restraints
set orient0 ( type cl .or. type ol .or. type nl .or. type ca )
set orient1 ( type cl .or. type ol .or. type nl .or. type ca )
!set orient0 ( .not. hydrogen )
!set orient1 ( .not. hydrogen )

!set orient0 ( all )
!set orient1 ( all )

set S string smcv

@S frames add sele segid dia .and. @orient0 end
@S frames add sele segid dia1 .and. @orient1 end
@S frames list
@S frames fill
@S frames print rest name frames0.dat ! should be identical

!goto skipq
! add orientation quaternion
@S add quaternion f1 1 f2 2
@S set weight 1 all
@S set force 10000 ind 1 ! hold cosine of half-rotation angle near zero (should be sufficient)
!@S set force 1000 all
@S list
label skipq
!stop
!goto skipx
! add position variables : dia1 in the frame of dia
@S add posi_com_x frame 1 sele segid dia1 .and. .not. hydrogen end force 10 wgt 1
@S add posi_com_y frame 1 sele segid dia1 .and. .not. hydrogen end force 10 wgt 1
@S add posi_com_z frame 1 sele segid dia1 .and. .not. hydrogen end force 10 wgt 1
! equivalent addition of position cv via explicit method :
!@S add posi_com frame 1 sele segid dia1 .and. .not. hydrogen end force 10 wgt 1 vector 1 0 0 ! x
!@S add posi_com frame 1 sele segid dia1 .and. .not. hydrogen end force 10 wgt 1 vector 0 1 0 ! y
!@S add posi_com frame 1 sele segid dia1 .and. .not. hydrogen end force 10 wgt 1 vector 0 0 1 ! z
!@S add posi_com frame 1 sele segid dia1 .and. .not. hydrogen end force 10 wgt 1
@S list
label skipx
! test gradients
@S test grad step 0.01

@S fill col main comp     ! new coordinates from comp set
!@S copy main dyna
@S fill col dyna          ! old coordinates from main set

@S print name cv0.dat rest col dyna
@S print name cv1.dat rest col main ! quaternions should be the same

@S stat hist hnam diala2_smd.dat ! hcol finst
energy
!stop
!========================================================================================
! for dynamics
scalar fbeta set 0 select all end
scalar fbeta set 10 select .not. hydrogen end ! do not apply langevin dynamics to hydrogens under shake
set tstep 0.001
set tstep 0.0005
set Temp 300

!!!!!!!!!!!!!!!!!! string options
!set nsteps 500000
set nsteps 100000
!
set reqfreq @nsteps ! @nsteps ! steps to adjust to new restraints
set freq 1000
set dcdfreq @freq ! trajectory output frequency
set save 10       ! frequency of saving statistics

! set up dcd & restart
string open write file unit 68 name @outputdir/diala@nrun.dcd
string open write card unit 69 name @outputdir/diala@nrun.rst

string colvar dyna -
 rstr reeq @reqfreq - ! restrained dynamics 
 smd -
 hisf @save -        ! time slices
 stat staf @freq -   ! statistics output
- ! dynamics options follow
lang tbath @Temp ilbfrq 0 rbuffer 0 -
strt -
- ! restrt
nstep @nsteps -
timestep @tstep -
iprfrq 1000 -
ihtfrq 0 -
ieqfrq 0 -
ntrfrq 0 -
iunrea -1 iunwri 69 iuncrd 68 iunvel -1 kunit -1 -
nprint 1000 -
nsavc @dcdfreq -
isvfrq @dcdfreq - ! frequency for writing a restart file
nsavv 0 -
inbfrq -1 -                        ! frequency of nb list gneration
iasors 1 - ! velocity scaling option at ihtfrq & iefrq freq. ; 0 -- scaling; ne 0 ==> assign (iasvel); not used in assignmnt
iasvel 1 - ! velocity assgn during heating/equil +  initial; >0 : Gaussian, <0 uniform, 0 from COMP set
iscvel 0 - ! velocity scaling: 0 -- same factor for all atoms; >0 different factor for each atom based on KE.

@S test grad step 0.01

@S print name cv_cur.dat col finst

