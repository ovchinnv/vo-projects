* CHARMM manual string update/reparametrization w/o rtf/top/psf
*

!********************* DUMMY Parameters
read rtf card
* Minimal topology
*
31  1

MASS  1   DUM   1.0 H

RESI DUM       0.00
GROUP
ATOM DUM  DUM  0.00
PATCHING FIRST NONE LAST NONE

END

read para card
* Mimimal parameters
*

NONB

DUM       0.0       0     1

END

read sequ DUM @{NCV}
generate DUM

!stop

if @?{smcv_old_local} eq 0 set smcv_old_local cv  !
if @?{smcv_new} eq 0 set smcv_new new         ! new string
if @?{smcv_first} eq 0 set smcv_first first   ! first string for rmsd

! need to initialize multicom if compiled in parallel
if ?PARALLEL eq 1 mcom string 1 by ?numnode
set S string smcv ! command shortcut
!**************************** replica variables ***********
@S init
set ncpu ?nstring
set me ?mestring
!**********************************************************
! define dummy cvs
set icv 1
label cvdef
 @S add POSI_COM_X sele bynum @icv end
 incr icv
 if @icv .le. @{ncv} -
goto cvdef
!
nobomb ! tolerate non-default behaviour
!
! read initial string for comparison (rmsd)
@S read restart name @{smcv_first} col ref ! read coordinates into reference array
! update string
! read individual files
@S read name @{smcv_old_local}_@{me}.dat local
! read CV weights -- no need because read ref string above
!@S weight read name wgt0.dat
! reparametrize
@S repa @{repatype} ! initialize
@S repa ! compute
! write new string file
@S write restart name @{smcv_new}
!
! compute string statistics
! set file append options
if @nrun eq 0 then
 set rapp
 set aapp
 set capp
else
 set rapp rapp
 set aapp aapp
 set capp capp
endif
@S stat - ! initialize
      rmsd rnam @{datadir}/rmsd.dat @{rapp} - ! compute rmsd from reference coordinate
      arcl anam @{datadir}/arcl.dat @{aapp} - ! compute arclength
      curv cvnm @{datadir}/curv.dat @{capp} - ! compute curvature

@S stat ! compute
