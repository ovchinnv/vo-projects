* Reads psf file & sets up for dynamics or minimization
*

nobomb
if @?version eq 0 set version 36
if @version ne 36 error WRONG PARAMETER VERSION : REQUESTED @version, PROVIDING 36
if @?resid eq 0 then
 set resid
else
 set resid resid
endif

if @?toppar eq 0 set toppar   ~/charmm/parameters/c36
if @?topparcgenff eq 0 set topparcgenff   ~/charmm/parameters/cgenff2b8
!
if @?format eq 0 set format card
!
if @?string eq 0 then 
 set string
else
 set string string
endif
!
!!!!!!!! read files
!==============================================================
! protein
if @?imm1 eq 0 then
 @string open unit 1 read form name "@TOPPAR/top_all36_prot.rtf"
 read rtf card unit 1
 @string close unit 1
 @string open unit 1 read form name "@TOPPAR/par_all36_prot.prm"
 read param card flex unit 1
 @string close unit 1
else
 @string open unit 1 read form name "@TOPPAR/top_all36_prot_lipid_eef1.1.rtf"
 read rtf card unit 1
 @string close unit 1
 @string open unit 1 read form name "@TOPPAR/par_all36_prot_lipid_eef1.1.prm"
! read param card unit 1
 read param card unit 1 flex
 @string close unit 1
endif
!
!stop
!===============================================================
! lipids, if needed
label lipid
if @?lipid eq 0 then
 set lipid 0
else
 set lipid 1
endif
!===============================================================
! carbohydrates, if needed
label carb
if @?carb eq 0 then
 set carb 0
else
 set carb 1
endif
!===============================================================
! nucleotides, if needed
label nucl
if @?nucl eq 0 then
 set nucl 0
else
 set na 1
 set nucl 1
endif
!===============================================================
! nucleic acids, if needed
label na
if @?na eq 0 then
 set na 0
else
 set na 1
endif
if @na eq 1 then
 @string open unit 1 read form name "@TOPPAR/top_all36_na.rtf"
 read rtf card unit 1 append
 @string close unit 1
!
 @string open unit 1 read form name "@TOPPAR/par_all36_na.prm"
 read param card flex unit 1 append
 @string close unit 1
endif
!
if @lipid eq 1 then
 @string open unit 1 read form name "@TOPPAR/top_all36_lipid.rtf"
 read rtf card unit 1 append
 @string close unit 1
!
 @string open unit 1 read form name "@TOPPAR/par_all36_lipid.prm"
 read param card flex unit 1 append
 @string close unit 1
endif
!
if @carb eq 1 then
 @string open unit 1 read form name "@TOPPAR/top_all36_carb.rtf"
 read rtf card unit 1 append
 @string close unit 1
!
 @string open unit 1 read form name "@TOPPAR/par_all36_carb.prm"
 read param card flex unit 1 append
 @string close unit 1
endif
!
if @nucl eq 1 then
 @string open unit 11 read form name @TOPPAR/stream/toppar_all36_na_nad_ppi.str
 stream unit 11 parser local
 @string open read card unit 1 name @TOPPAR/gtp.rtf ! gtp topo
 read rtf card unit 1 append
 @string close unit 1
 @string open read card unit 1 name @TOPPAR/gdp.rtf ! gdp topo
 read rtf card unit 1 append
 @string close unit 1
endif
!
!==============================================================
! cgenff
label cgenff
if @?cgenff eq 0 then 
 set cgenff 0
else
 set cgenff 1
endif
if @cgenff eq 1 then 
 @string open unit 1 read form name "@TOPPARCGENFF/top_all36_cgenff.rtf"
 read rtf card unit 1 append
 @string close unit 1
 @string open unit 1 read form name "@TOPPARCGENFF/par_all36_cgenff.prm"
 read param card flex unit 1 append
 @string close unit 1
endif
!
!======= water and ions ========================================
label water
if @?nowat eq 0 then
 @string open read card unit 11 name @TOPPAR/toppar_water_ions.str
 stream unit 11 parser local
endif
!
!====== additional parameters if requested =====================
if @?adpar ne 0 then
 @string open unit 1 read form name "@{ADPAR}.rtf"
 read rtf card unit 1 append
 @string close unit 1
 @string open unit 1 read form name "@{ADPAR}.prm"
 read param card flex unit 1 append
 @string close unit 1
endif
!===============================================================
if @?adstr ne 0 then
 @string open unit 11 read form name "@{ADSTR}"
 stream unit 11 parser local
endif
!===============================================================
!
label read_psf
if @?psffile eq 0 return
@string open unit 1 read @format name @psffile.psf
read psf @format unit 1
@string close unit 1
if @?cor eq 0 set cor 1 
if @?crd eq 0 set crd 1 
if @?pdb eq 0 set pdb 1

if @?corfile eq 0 set cor 0
if @?crdfile eq 0 set crd 0
if @?pdbfile eq 0 set pdb 0

if @cor ne 0 @string open unit 1 read @format name @corfile.cor
if @cor ne 0 read coords @format unit 1
!
if @crd ne 0 @string open unit 1 read @format name @corfile.crd
if @crd ne 0 read coords @format unit 1
!
if @pdb ne 0 @string open unit 1 card read name @pdbfile.pdb
if @pdb ne 0 read coords pdb unit 1 @resid
@string close unit 1
