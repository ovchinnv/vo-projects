* delete waters and ions to obtain the same number of atoms in each path image psf
*

set nfiles 32
set basename temp/rab11w-o_
set ext n

set maxwat 9727 ! from output of numwat
set maxsod 14
set maxcla 11

set dx 1 ! decrement for cutting the box size


set ifile 0

label psf

 set psfname @{basename}@{ifile}@{ext}
 set corname @psfname
!
 read psf card name @{psfname}.psf
 read coor card name @{corname}.cor
!
 coor stat
!
 calc x0 = abs ( ?xmin )
 calc x1 = abs ( ?xmax )

 calc y0 = abs ( ?ymin )
 calc y1 = abs ( ?ymax )

 calc z0 = abs ( ?zmin )
 calc z1 = abs ( ?zmax )
 
 set max @x0
 if @max lt @x1 set max @x1
 if @max lt @y0 set max @y0
 if @max lt @y1 set max @y1
 if @max lt @z0 set max @z0
 if @max lt @z1 set max @z1
 
 if @max eq @x0 then
  set prop x
  set cut ?xmin
  set dir 1
  set op .lt.
 endif

 if @max eq @x1 then
  set prop x
  set cut ?xmax
  set dir -1
  set op .gt.
 endif

 if @max eq @y0 then
  set prop y
  set cut ?ymin
  set dir 1
  set op .lt.
 endif

 if @max eq @y1 then
  set prop y
  set cut ?ymax
  set dir -1
  set op .gt.
 endif

 if @max eq @z0 then
  set prop z
  set cut ?zmin
  set dir 1
  set op .lt.
 endif

 if @max eq @z1 then
  set prop z
  set cut ?zmax
  set dir -1
  set op .gt.
 endif

 calc dd = @dx * @{dir}

 define wat sele segid *WAT end
 calc nremove = ?nsel - @maxwat * 3 ! number of water atoms to remove

 format (1F17.10) ! increase precision for this part of calculation
 label sel
  define extra sele .byres. ( segid *WAT .and. prop @prop @op @cut ) end
  if ?nsel .lt. @nremove then
   calc cut = @cut + @dd
   goto sel
  endif
!
 if ?nsel .gt. @nremove then
   calc cut = @cut - @dd
   calc dd = @dd / 2
   goto sel
 endif
 format(1PG14.6)
! just right : delete extra waters
 define delwat sele extra end
! select ions to delete
 define sod sele type sod end
 calc nremove = ?nsel - @maxsod
 define delsod sele type sod .and. resid 1:@nremove end
!
 define cla sele type cla end
 calc nremove = ?nsel - @maxcla
 define delcla sele type cla .and. resid 1:@nremove end
! delete : 
 define todelete sele delwat .or. delsod .or. delcla end
 if ?nsel gt 0 then
  delete atom sele todelete end
  join renu wat
  join renu ion1
  join renu ion2
 endif
!
 write psf card name @{psfname}d.psf
 write coor card name @{corname}d.cor
!
 incr ifile
 if @ifile lt @nfiles -
goto psf

