module state
 __DEP_OUTPUT
!
 type varray
   float, pointer, dimension(:,:,:) :: v
 end type varray
!
 int, parameter :: nvar=4
 character(len=6), parameter :: var_names(nvar) = &
#ifndef __PATHSCALE
& [character(len=6) :: 'EPS','KAPPA','CHARGE', 'PHI'];
#else
& (/'EPS   ','KAPPA ','CHARGE', 'PHI   '/);
#endif
 bool :: var_init(nvar)
!
 float, pointer, save, dimension(:,:,:) :: &
&   p, & ! electrostatic potential
&   eps, & ! dielectric constant
&   kappa, & ! ionic strength parameter
&   rhs  ! charge density (source terms)
!
 bool, save :: state_initialized=.false.
!
 public state_initialize
 public state_done
!
 contains
!*******************************************************************
  subroutine state_initialize()
  use SIZE, only: nx, ny, nz, size_initialized, me, communicator
  __DEP_PARSER
  __DEP_OUTPUT
  use files
  use formats
  __IMPNONE
!
  character(len=16) :: whoami='STATE_INITIALIZE';
!
  type (varray), dimension(nvar) :: vars
  int :: i, j, k, l, fid, ifmt
  float :: value
  bool :: qbin
  character(len=80) :: filename, fmt, file_mode, file_format
!
  if (state_initialized) call state_done()
  if (.not. size_initialized) then
   call warning(whoami, 'SIZE NOT INITIALIZED. NOTHING DONE.', -1)
   return
  else
   allocate(p(nx,ny,nz),eps(nx,ny,nz),kappa(nx,ny,nz),rhs(nx,ny,nz))
   vars(1)%v=>eps
   vars(2)%v=>kappa
   vars(3)%v=>rhs
   vars(4)%v=>p
!
! check for assignments to eps, kappa, and charge (at present, this conflicts with 'molecule.src', in that you cannot use both assignments)
! note that the boundary points are read, too; this is necessary because interpolate epsilon & kappa values are needed at cell boundaries
!
   do i=1,nvar
! check if initialization is specified
    var_init(i)=.false.
    l=len_trim(var_names(i))
    select case(getval_nocase_upper(var_names(i)(1:l)//'INIT'))
     case('CONST','CONSTANT')
      if (existtag_nocase(var_names(i)(1:l)//'CONSTANT')) then
       vars(i)%v=atof(getval_nocase(var_names(i)(1:l)//'CONSTANT'))
       var_init(i)=.true.
      elseif (existtag_nocase(var_names(i)(1:l)//'CONST')) then
       vars(i)%v=atof(getval_nocase(var_names(i)(1:l)//'CONST'))
       var_init(i)=.true.
      else
       call warning(whoami, 'Initialization constant for variable "'//var_names(i)(1:l)//'" not specified',-1)
      endif
     case('FILE')
      if (existtag_nocase(var_names(i)(1:l)//'FILE')) then
       filename=getval_nocase(var_names(i)(1:l)//'FILE')
       call adjustleft(filename)
       k=len_trim(filename)
       call message(whoami, 'Will initialize variable "'//var_names(i)(1:l)//'" from file "'//filename(1:k)//'"')
      else
       call warning(whoami, 'Initialization file for variable "'//var_names(i)(1:l)//'" not specified',-1)
      endif
!
      if (existtag_nocase(var_names(i)(1:l)//'_MODE')) then
        file_mode=getval_nocase_upper(var_names(i)(1:l)//'_MODE')
        select case(file_mode);
         case('BINARY', 'BIN');      call message(whoami,'Using binary mode');     qbin=.true.
         case('ASCII', 'TEXT');      call message(whoami,'Using ASCII mode');      qbin=.false.
         case default ;  call warning(whoami,'Unknown output mode "'//file_mode(1:len_trim(file_mode))//'" requested',-1);
         qbin=.false.
        end select
      else
        qbin=.true.
      endif
!
      ifmt=chest ! default format
      if (existtag_nocase(var_names(i)(1:l)//'_FORMAT')) then
        file_format=getval_nocase_upper(var_names(i)(1:l)//'_FORMAT');
        ifmt=-999
        do j=1,num_fmt
         if (file_format.eq.format_name(j)) then 
          ifmt=j
          exit
         endif
        enddo
      endif
!
      if (ifmt.gt.0) then
       call message(whoami,format_name(ifmt)(1:len_trim(format_name(ifmt)))//' format will be used');
      else
       call warning(whoami,'Format "'//file_format(1:len_trim(file_format))//'" is not recognized',-1)
      endif
!
!      if (me.le.0) &
!       call message(whoami, 'Reading data for variable "'//var_names(i)(1:l)//'" from file '//filename(1:k))
!
      select case(ifmt)
       case(plot3d); call plot3Dread_scalar(filename,vars(i)%v,nx,ny,nz,1,qbin)
       case(chest);  call chest_read_scalar(filename,vars(i)%v,nx,ny,nz,qbin)
      end select
!
      if (.not.fatal_warning(__COMM)) var_init(i)=.true.
     case('OBJECT') ! do nothing : expect that object_grid_parameters will take care of correct initialization
      call message(whoami, 'Initialization for variable "'//var_names(i)(1:l)//'" will be computed from object')
      var_init(i)=.true.
     case default
      call warning(whoami, 'Initialization option for variable "'//var_names(i)(1:l)//'" not recognized',-1)
    end select     
!
   enddo ! over all variables
!
   if (all(var_init)) state_initialized=.true.
!
  endif ! size_initialized
!
  end subroutine state_initialize
!*******************************************************************
  subroutine state_done()
  __IMPNONE
!
  if (state_initialized) then
   deallocate(p,eps,kappa,rhs)
   state_initialized=.false.
  endif
!
  end subroutine state_done
!******************************************************************
end module state
