      module anglpar
      __IMPNONE
!    define a derived type to store angle parameters
      type angles
       character(len=8), dimension(:), pointer :: a1, a2, a3
       float, dimension(:), pointer :: ktheta, theta0, kub, s0
! V(angle) = Kb(b - b0)**2
!Kb: kcal/mole/A**2
!b0: A
       int :: length ! length of the array
       int :: last ! index of last element
       bool :: initialized=.false. ! has the vector been initialized
      end type angles
!
      int, parameter, private :: expand_incr=200
!
      contains
!==================================================================
       subroutine angles_init( v )
       type (angles) :: v
!       if (associated(v%...)) deallocate(v%...) ! testing unassigned pointer is an error!
       allocate(v%a1(expand_incr),v%a2(expand_incr),v%a3(expand_incr),v%ktheta(expand_incr),v%theta0(expand_incr),&
&                v%kub(expand_incr),v%s0(expand_incr))
!
       v%a1=''; v%a2=''; v%a3=''; v%ktheta=0; v%theta0=0; v%kub=0; v%s0=0
       v%length=expand_incr
       v%last=0
       v%initialized=.true.
       end subroutine angles_init
!==================================================================
       subroutine angles_done( v )
       type (angles) :: v
       if (associated(v%a1)) deallocate(v%a1)
       if (associated(v%a2)) deallocate(v%a2)
       if (associated(v%a3)) deallocate(v%a3)
       if (associated(v%ktheta)) deallocate(v%ktheta)
       if (associated(v%theta0)) deallocate(v%theta0)
       if (associated(v%kub)) deallocate(v%kub)
       if (associated(v%s0)) deallocate(v%s0)
       v%length=0
       v%last=0
       v%initialized=.false.
       end subroutine angles_done
!==================================================================
       subroutine angles_expand( v )
       type (angles) :: v
       int :: newlength
       character(len=8), dimension(:), pointer :: a1, a2, a3
       float, dimension(:), pointer :: kub, s0, ktheta, theta0
!
       if (.not.v%initialized) then 
        call angles_init(v) 
       else
!    assume length is valid
        newlength=v%length+expand_incr ! temporary storage space
        allocate(a1(newlength),a2(newlength),a3(newlength),kub(newlength),s0(newlength),&
&                 ktheta(newlength),theta0(newlength)) ! copy old data
        a1(1:v%length)=v%a1; a2(1:v%length)=v%a2; a3(1:v%length)=v%a3; kub(1:v%length)=v%kub; s0(1:v%length)=v%s0
        ktheta(1:v%length)=v%ktheta; theta0(1:v%length)=v%theta0
        deallocate(v%a1,v%a2,v%a3,v%ktheta,v%theta0,v%kub,v%s0)
        v%a1=>a1; v%a2=>a2; v%a3=>a3; v%ktheta=>ktheta; v%theta0=>theta0; v%kub=>kub; v%s0=>s0;
        v%length=newlength
       endif 
       end subroutine angles_expand
!==================================================================
       function angles_add(v,a1,a2,a3,ktheta,theta0,kub,s0)
       type (angles) :: v
       int :: angles_add
       float :: ktheta, theta0, kub, s0
       character(len=*) :: a1, a2, a3
       int :: j
!
       if (.not.v%initialized) call angles_init(v) 
!    add element to the list
       if (v%last.eq.v%length) call angles_expand(v)       
       j=v%last+1
       v%a1(j)=a1; v%a2(j)=a2; v%a3(j)=a3; v%ktheta(j)=ktheta; v%theta0(j)=theta0; v%kub(j)=kub; v%s0(j)=s0
       v%last=j
       angles_add=j
       end function angles_add
!==================================================================
       function angles_uadd(v,a1,a2,a3,ktheta,theta0,kub,s0) 
       use output, only: warning
       type (angles) :: v
       int :: angles_uadd
       float :: ktheta, theta0, kub, s0
       character(len=*) :: a1, a2, a3
       int :: j
!
       if (.not.v%initialized) call angles_init(v) 
       do j=1,v%last
        if ( v%a2(j).eq.a2.and.((v%a1(j).eq.a1.and.v%a3(j).eq.a3).or.(v%a1(j).eq.a3.and.v%a3(j).eq.a1)) ) then
!       found element
         angles_uadd=j
         call warning('ANGLES_UADD','Parameters for angle "'//a1//'--'//a2//'--'//a3//'" already present. Will overwrite.',0)
         v%ktheta(j)=ktheta; v%theta0(j)=theta0; v%kub(j)=kub; v%s0(j)=s0
         return
        endif
       enddo
!    add element to the list: use regular routine
       angles_uadd=angles_add(v,a1,a2,a3,ktheta,theta0,kub,s0)
!
       end function angles_uadd
!==================================================================
       function angles_getind( v,a1,a2,a3 ) 
       type (angles) :: v
       int :: angles_getind
       character(len=*) :: a1, a2, a3
       int :: j
!
       angles_getind=-999
       if (.not.v%initialized) then 
!
       else
        do j=1,v%last
         if ( v%a2(j).eq.a2.and.((v%a1(j).eq.a1.and.v%a3(j).eq.a3).or.(v%a1(j).eq.a3.and.v%a3(j).eq.a1)) ) then
!       found element
          angles_getind=j
          exit
         endif
        enddo
       endif
!
       end function angles_getind
      end module anglpar
